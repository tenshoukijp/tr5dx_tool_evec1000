太閤立志伝５イベントソース
// セーブナンバーの実験用のイベント
// 座に入るとセーブナンバーで実現した金庫からお金を出し入れできる
// コンバートにはDX版の千階堂ツールが必要

チャプター: {
	イベント:セーブナンバー金庫 {
		属性:何度でも
		発生契機:室内画面表示後(無効,座)
		発生条件: {
		}

		スクリプト: {
			// ゲーム選択
			ナレーション:[[セーブナンバー金庫に対する操作を選んでください]]
			選択:[[入金]][[出金]][[何もしない]]
			分岐:([[入金]]) {
				// メッセージ
				代入ａ:(0)
				文字列設定:(Ｅｍｐｔｙ)[[＠千＠代入＠ａ＠セ：：セーブナンバー金庫]]
				ナレーション:[[現在<ａ>貫預かっています]]
				// 数値入力
				代入ｂ:(0)
				代入ｃ:(人物::主人公.所持金)
				文字列設定:(Ｅｍｐｔｙ)[[＠千＠数値入力＠ｂ＠0＠ｃ＠0＠貫＠いくら入金しますか？]]
				// 入金処理
				代入ｄ:(ｃ)-(ｂ)
				更新:(主人公.所持金)(ｄ)
				代入ｅ:(ａ)+(ｂ)
				文字列設定:(Ｅｍｐｔｙ)[[＠千＠更新＠セ：：セーブナンバー金庫＠ｅ]]
				// メッセージ
				ナレーション:[[現在<ｅ>貫預かっています]]
			}
			分岐:([[出金]]) {
				// メッセージ
				代入ａ:(0)
				文字列設定:(Ｅｍｐｔｙ)[[＠千＠代入＠ａ＠セ：：セーブナンバー金庫]]
				ナレーション:[[現在<ａ>貫預かっています]]
				// 数値入力
				代入ｂ:(0)
				文字列設定:(Ｅｍｐｔｙ)[[＠千＠数値入力＠ｂ＠0＠ａ＠0＠貫＠いくら出金しますか？]]
				// 出金処理
				代入ｃ:(人物::主人公.所持金)
				代入ｄ:(ｃ)+(ｂ)
				更新:(主人公.所持金)(ｄ)
				代入ｅ:(ａ)-(ｂ)
				文字列設定:(Ｅｍｐｔｙ)[[＠千＠更新＠セ：：セーブナンバー金庫＠ｅ]]
				// メッセージ
				ナレーション:[[現在<ｅ>貫預かっています]]
			}
			分岐:([[何もしない]]) {
			}
		}
	}
}